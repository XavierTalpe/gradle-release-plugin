buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


/**
 * Project dependencies.
 */
repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.eclipse.jgit:org.eclipse.jgit:3.5.1.201410131835-r'

    testCompile 'junit:junit:4.11'
}

compileGroovy {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}


/**
 * Artifacts configuration.
 */
task buildSourcesJar( type: Jar, dependsOn: classes ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives buildSourcesJar
}


/**
 * For the integration tests we require the jar to
 * be available before the tests are started.
 */
tasks.test.dependsOn jar

/**
 * Maven publication settings.
 */
publishing {
    publications {
        gradleReleasePlugin( MavenPublication ) {
            from components.java

            artifact buildSourcesJar {
                classifier 'sources'
            }
        }
    }
}


/**
 * Uploading plugin to Gradle Plugin portal.
 */
bintray {
    user = bintrayUser
    key = bintrayApiKey

    publications = ['gradleReleasePlugin']

    dryRun = false
    publish = false

    pkg {
        repo = bintrayRepo
        userOrg = bintrayUser
        name = project.name
        desc = project.description

        websiteUrl = 'https://github.com/XavierTalpe/gradle-release-plugin'
        issueTrackerUrl = 'https://github.com/XavierTalpe/gradle-release-plugin/issues'
        vcsUrl = 'https://github.com/XavierTalpe/gradle-release-plugin.git'

        licenses = ['MIT']
        labels = ['gradle', 'release', 'plugin', 'automatic', 'version', 'git']

        publicDownloadNumbers = true

        version {
            name = project.version
            vcsTag = project.version
            attributes = ['gradle-plugin': "be.xvrt.release:${project.group}:${project.name}"]
        }
    }
}

tasks.bintrayUpload.dependsOn test
